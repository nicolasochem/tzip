---
tzip: A1
title: Michelson Contract Interfaces and Conventions
status: WIP
type: Application
author: John Burnham
advocate: John Burnham
created: 2019-04-11
---

## Summary

This document defines patterns that make it easier for developers to build smart
contracts in Michelson, especially when those contracts have to communicate with
one another.

## Abstract

Michelson is a low-level typed stack-based functional programming language,
which is an unusual combination of features, to put it mildly. While Michelson
is surprisingly expressive for a language of its simplicity, it lacks many
generally typical programming language features, such as variables or
references. This leads to developer discomfort, limits contract complexity and
inhibits interoperability of contracts written by different individuals or
teams. The problem of interoperation is particularly exacerbated with regards to
higher level languages that generate or compile to Michelson, since the
Michelson code produced by two different compilers or generators can have
radically different structure.

Fortunately, Michelson's expressivity allows us to define common conventions,
abstractions and best practices which we hope will mitigate these challenges. We
hope these patterns will prove informative and useful to Michelson developers,
as well as developers of languages and tools which target Michelson.

**Patterns defined in this document**:

- Canonical Normal Form
- Canonical Annotated Form and Entry-point Sums
- ADT syntax sugar
- CASE macro
- View Entry-points
- Void Entry-points
- Entry-point Callback Feathering

## Canonical Normal Form

The canonical normal form of a Michelson type is defined as the type
representation which distributes all `pair` types over `or` types. That is to
say, the type is represented as a [sum of products].

Consider the following Michelson type:

```
(pair int (or nat bool))
```

This type is structurally equivalent to:

```
(or (pair int nat) (pair int bool))
```

However, the second type is in canonical form, wheras the first is not.

Canonical form can be thought of as analogous to the distribution of
multiplication over addition in arithmetic:

```
z * (x + y) = x * z + y * z
```

While both terms are equal, the fully distributed form has some advantages when
it comes to *computation*, since it enables all products or summands to be
considered independently.

Equivalently, canonical form in a Michelson contract's parameter type enables us
to write logic which considers each possible case of our `or` union type
separately. This enables construction of independent *entry-points* to our
contract.

## Entry-points and Canonical Annotated Form

The entry-points of a Michelson contract are the branches of its `parameter`
type in *canonical annotated form*, where each entry point corresponds to an
annotated variant.

The distinction between *canonical annotated form* and *canonical normal form*
is that in *CAF*, only `or` types containing annotated variants must be
distributed over all product types. Unannotated `or` types are considered as
single arguments to an entry-point rather than exporting entry-points themselves

For examples `(or (unit %foo) (pair %bar unit (or int bool))` is not in *CNF*,
but is in *CAF*, and therefore exports two entry-points `%foo` which is of type
`unit` and `%bar` which is of type `(pair unit (or int bool)`.

### Example

Consider the following simple Michelson contract that implements a
counter:

```
parameter
  (or
    unit %bump_counter
    nat  %reset_counter
  );
storage nat;
code {UNPAIR;
      IF_LEFT
        {DROP; PUSH nat 1; ADD; NIL operation; PAIR;} # %bump_counter
        {DIP {DROP}; NIL operation; PAIR;}            # %reset_counter
     };
```

This contract exports two entry points: `%bump_counter` and `%reset_counter`,
which add one to the `nat` in storage, or replace the `nat` with a new `nat`.

Notice that after we enter the `IF_LEFT` in the `code` block, the two branches
of the contract are completely logically separate from one another.

The sequence of instructions for `%bump_counter` has type

```
{DROP; PUSH nat 1; ADD; NIL operation; PAIR;}

 :: unit : nat : [] -> pair (list operation) nat : []
```

The sequence of instructions for `%reset_counter` has type

```
{DIP {DROP}; NIL operation; PAIR;}

  :: nat : nat : [] -> pair (list operation) nat : []
```

In a sense, each entrypoint almost acts like it's its own contract. We can
imagine a contract that implements only `%bump_counter`:

```
parameter unit;
storage nat;
code { UNPAIR; DROP; PUSH nat 1; ADD; NIL operation; PAIR;}
```

and a contract that implements only `%reset_counter`:

```
parameter nat;
storage nat;
code {UNPAIR; DIP {DROP}; NIL operation; PAIR;}
```

Our entry-point pattern allows us to combine both these separate contracts into
a single contract with a two "methods" that operate on a common storage.

If we now want to add a third method to our counter contract called
`%antibump_counter`, we can extend the union by nesting another `or` type
constructor:

```
parameter
 (or
    unit   %antibump_counter
    (or
      unit %bump_counter
      nat  %reset_counter
    )
  );
storage nat;
code {UNPAIR;
      IF_LEFT
        {DROP; PUSH nat 1; SUB; ISNAT; ASSERT_SOME;
         NIL operation; PAIR
        }                                               # %antibump_counter
        {IF_LEFT
          {DROP; PUSH nat 1; ADD; NIL operation; PAIR;} # %bump_counter
          {DIP {DROP}; NIL operation; PAIR;}            # %reset_counter
        };
     };
```

This pattern can be extended further to `n` entrypoints by adding additional
`or` types and `IF_LEFT` instructions.

This standard does not define a conventional structure that *CAF* `or`-trees
must follow. So long as the `parameter` is in *CAF* form, any tree structure is
compatible. Some examples are:

- Right-hand comb:`(or a (or b (or c (or d e))))`
- Left-hand comb:`(or (or (or (or a b) c) d) e)`
- Right-hand balanced tree: e.g `(or (or a b) (or c (or d e)))`
- Left-hand balanced tree: e.g `(or (or (or a b) c) (or d e))`

All such structures are `A1` compliant, so long as all entry points are indicated
with fields, and all union type arguments are unannotated.

Nevertheless it is advisable that specific implementations adhere to a
consistent tree-struct convention, examples of which can be seen in various
extensions of this standard (such as `TZIP-A1.1`).

--- 

** NOTE: The following standards described for *ADT syntax sugar*, and `CASE`,
`VIEW` and `VOID` patterns are described here for a right-hand comb `or`-tree
structure. Implementations following a different tree structure convention
should adapt these standards accordingly.

## ADT syntax sugar

To increase syntactic clarity, we propose a Abstract Data Type syntactical sugar
for `pair` and `or` types according to the following reduction rules:

```
(a, b)                            ~> (pair a b)
(a, b) :t %f                      ~> (pair :t %f a b)
(a, b, c)                         ~> (a, (b, c))
(a, b, c) :t %f                   ~> (a, (b, c)) :t %f
(a :ta %fa, b :tb %fb, c :tc %fc) ~> (a :ta %fa, (b :tb %fb, c :tc %fc))

(a | b)                             ~> (or a b)
(a | b) :t %f                       ~> (or :t %f a b)
(a | b | c)                         ~> (a | (b | c))
(a | b | c) :t %f                   ~> (a | (b | c)) :t %f
(a :ta %fa | b :tb %fb | c :tc %fc) ~> (a :ta %fa | (b :tb %fb | c :tc %fc))
```

Types written in this syntax sugar should relax any Michelson implementations
indentation and whitespacing rules.

For example, In this sugar, the parameter type of our above counter contract
could be written:

```
parameter
  ( unit %antibump_counter
  | unit %bump_counter
  | nat  %reset_counter
  );
```

# CASE macro

We propose a new multiary macro called `CASE`:

```
CASE a b (\rest) / S => IF_LEFT a {CASE b (\rest)} / S
CASE a b / S => IF_LEFT a b / S
```

This enables the code in the counter contract to be rewritten:

```
code {UNPAIR;
      CASE
        {DROP; PUSH nat 1; SUB; ISNAT; ASSERT_SOME; NIL operation; PAIR }
        {DROP; PUSH nat 1; ADD; NIL operation; PAIR;}
        {DIP {DROP}; NIL operation; PAIR;}
     };
```

[WIP TODO: How to implement?]
Optionally, the branches in `CASE` may be annotated with field annotations:

```
code {UNPAIR;
      CASE
       %antibump_counter {DROP; PUSH nat 1; SUB; NIL operation; PAIR;}
       %bump_counter     {DROP; PUSH nat 1; ADD; NIL operation; PAIR;}
       %reset_counter    {DIP {DROP}; NIL operation; PAIR;}
     };
```

which should generate a type error if the field annotations don't match.

## View Entry Points

We define the following synonym in a contract's parameter type declaration:

```
view a r = (a, contract (a, option r))
```

A `view` is an entry point which represents a computation that takes an argument
of type `a` and returns a result of type `r`. This return type is represented as
a callback. For example

```
parameter
  ( unit          %antibump_counter
  | unit          %bump_counter
  | nat           %reset_counter
  | view unit nat %getCount
  );
storage nat;
code {UNPAIR;
      CASE
        {DROP; PUSH nat 1; SUB; ISNAT; ASSERT_SOME; NIL operation; PAIR }
        {DROP; PUSH nat 1; ADD; NIL operation; PAIR;}
        {DIP {DROP}; NIL operation; PAIR;}
        {UNPAIR; DIP {AMOUNT; DUUUP; SOME;} PAIR;
         TRANFER_TOKENS; NIL operation; SWAP; CONS; PAIR}
     };
```

By convention, `view` must emit only a single transfer `operation` to the
callback contract passed by the caller, and must not mutate the contract storage
in any way.

The reason `view` is defined as `(a, contract (a, option r))` rather than
`(a, contract r)` is to enable `view`s to be generic across caller types via the
"callback feathering" pattern (see below)

To make the process of writing the logic for a `view` easier, we define a `VIEW`
macro:

```
VIEW :: view a r : storage : S -> (list operation, storage) : S
VIEW code =
  UNPAIR; DIP {DUUP}; DUP; DIP {PAIR; code; SOME;}; PAIR; DIP {AMOUNT};
  TRANSFER_TOKENS; NIL operation; SWAP; CONS; PAIR;
  where
    code :: (a, storage) : S -> r : S
```

This macro allows us to rewrite our `%getCount` entry point as

```
code {UNPAIR;
      CASE
        {DROP; PUSH nat 1; SUB; ISNAT; ASSERT_SOME; NIL operation; PAIR }
        {DROP; PUSH nat 1; ADD; NIL operation; PAIR;}
        {DIP {DROP}; NIL operation; PAIR;}
        {VIEW {CDR}}
     };
```

## Entry-point Callback Feathering

One challenge that arises from our construction of `view` entry-points is that
contracts whose parameters are unions cannot pass themselves as callbacks to
contracts with a view whose type is a variant or "arm" of the caller's parameter.

For example, a contract with parameter type `(or nat bool)` will be unable use
it's own address to call our counter contract's `%getCount` view, since
`contract (unit, option nat)` and `contract (or nat bool)` are different types. 
While we could simply extend our counter with a `(unit, contract (or nat bool))` 
this is impractical and breaks our `view` abstraction. Defining `view`s
in this way would greatly restrict the genericity of our `view` entry points,
since a different `view` entrypoint would have to be added for each possible
contracts with a parameter `nat` arm.

We define a technique using an intermediate contract called a
"feather" (named after the motion a rower makes to smoothly disengage their oar
blade from the water) that allows a caller to call any view whose return type
matches one of the caller's entry points. This is useful particularly if the
`view` contract is already on-chain and cannot be modified.

For any caller with a entry-point of type `r`, and
any callee with an entry-point of type `view a r`:

```
caller contract (r | ...)
callee contract (view a r | ...)
```

The feather is an intermediate contract of type:

```
feather = contract (a, option r)
```

When called by the caller with `(arg, None)`, where `arg` is an argument of type
`a`, the feather passes the call to the callee, with the feather address as a
callback.

The callee will then compute the view and re-enter the feather with
`(arg, option ret)` where `arg` is the original argument, and `ret` is the
return value of type `r`.

The feather will then embed `ret` into an appropriate union type corresponding
to the parameter of the caller, and re-enter the caller at the appropriate
entry-point. Internally, the feather may distinguish which of these functions to
perform via branching on the value of `SENDER` or on whether the `option ret`
type is `Some` or `None`.

## Void Entry Points

We define the folling synonym in a contract's parameter type declaration

```
void a b = (a, lambda b b)
```

We also define a `VOID` macro:

```
VOID :: void a b : S -> b : _
VOID code = UNPAIR; SWAP; DIP {code}; SWAP; EXEC; FAILWITH;
```

A `void` is an entry point that ends in a `FAILWITH b`. By construction they
cannot be run on-chain. `void`s are designed to be run locally and may be very
gas-expensive. They are included in the contract interface primarily to provide
enable trust-less computation on the contract's storage. That is, the presence
of the entry point on-chain implies that all parties can have certainty that
anyone who calls a `void` with identical arguments will compute the same result.

The `lambda b b` in the void definition is intended to be used as a type proxy,
and "callers" of void entry points should pass the identity function (the value
produced by `LAMBDA b b {}`) here. That said, it is possible that there may be
utility in some cases to using this parameter as a local continuation.

`void` entrypoints must occur at the end (the most deeply nested part) of the
parameter's union.

```
parameter
  ( unit            %antibump_counter
  | unit            %bump_counter
  | nat             %reset_counter
  | view unit nat   %getCount
  | void unit bytes %hashCount
  );
storage nat;
code {UNPAIR;
      CASE
        {DROP; PUSH nat 1; SUB; NIL operation; PAIR;}
        {DROP; PUSH nat 1; ADD; NIL operation; PAIR;}
        {DIP {DROP}; NIL operation; PAIR;}
        {CDR; AMOUNT; DUUUP; TRANFER_TOKENS; NIL operation; SWAP; CONS; PAIR}
        {VOID {DROP; PACK; SHA512}
     };
```


[sum of products]: https://en.wikipedia.org/wiki/Canonical_normal_form

let {

  type Parameter =
    ( (Address, Nat)              %transfer
    | (Address, Nat)              %mint
    | (Address, Nat)              %burn
    | View () Nat                 %getTotalSupply
    | View Address (Option Nat)   %getBalance
    );

  type Storage =
    (BigMap %ledger Address Nat
    , Nat     %totalSupply
    , Address %centralBank
    );

  getSourceBalance :: '[(Address, Nat), Storage]
                   -> '[Nat, (Address, Nat), Storage]
  = {duup; car; source; get; assert_some; };

  subtractBalance :: '[Nat, (Address, Nat), Storage]
                  -> '[Option Nat, (Address, Nat), Storage]
  = { duup; cdr; swap; sub; dup; assert_ge; 
      dup; eq; if {drop; none Nat} {isnat};
    };

  debitSource :: '[Option Nat, (Address, Nat), Storage] -> '[(Address, Nat), Storage]
  = { dip {swap; unpair}; source; update; pair; swap};

  getRecipientBalance :: '[(Address, Nat), Storage]
                      -> '[Option Nat, (Address, Nat), Storage]
  = { duup; car; duup; car; get};

  newRecipientBalance :: '[Option Nat, (Address, Nat), Storage]
                      -> '[Nat, (Address, Nat), Storage]
  = { if_some
      {duup; cdr; add}
      {dup; cdr};
    };

  updateRecipient :: '[Nat, (Address, Nat), Storage]
                  -> '[(Address, Nat), Storage]
  = { some; duup; car; diip {swap; unpair}; update; pair; swap;};

  sourceIsBank :: '[Storage] -> '[Bool, Storage]
  = { dup; cddr; source; cmpeq};

  mintBalance :: '[(Address, Nat), Storage]
              -> '[([Operation], Storage)]
  = { dip {sourceIsBank; assert;};
      getRecipientBalance; newRecipientBalance; updateRecipient;
      drop; nil Operation; pair;
    };

  burnBalance :: '[(Address, Nat), Storage]
              -> '[([Operation], Storage)]
  = { dip {sourceIsBank; assert;};
      getRecipientBalance;
      assert_some;
      subtractBalance;
      assert_some;
      updateRecipient;
      drop; nil operation; pair
    };

  transferBalance :: '[(Address, Nat), Storage]
                  -> '[(List Operation, Storage)]
  = { getSourceBalance; subtractBalance; debitSource;
      getRecipientBalance; newRecipientBalance; updateRecipient;
      drop; nil operation; pair;
    };

  getTotalSupply :: '[ View () Nat, Storage ] -> '[(List Operation, Storage)]
  = { view {cddar} };

  getBalance :: '[ View Address (Option Nat), Storage ]
             -> '[(List Operation, Storage)]
  = { view { unpair; dip {car}; get} };

};
parameter Parameter;
storage Storage;
code {unpair;
      case
        { transferBalance}
        { mintBalance}
        { burnBalance}
        { getTotalSupply }
        { getBalance };
     };
